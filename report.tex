\documentclass[12pt, a4paper]{article}

% Language

\usepackage[english]{babel}
\usepackage[english]{isodate}
\usepackage[utf8]{inputenc}

% Symbols

\usepackage{amsmath}
\usepackage{amssymb}

% Colors 

\usepackage{xcolor}

% Figures

% \usepackage{float}
% \usepackage{tikz}
% \usepackage{graphicx}
% \usepackage{framed}
% \usepackage{bussproofs}

% Layout

\usepackage[cm]{fullpage}
\setlength{\textheight}{640pt}
% \usepackage{layout}
\usepackage{fancyhdr}

% Misc

\usepackage{listings}
\lstset{
    language=ML,
    basicstyle=\scriptsize\tt\color{black!70},
    keywordstyle=\color{black!100},
    showstringspaces=false
}
\usepackage{alltt}
\usepackage{hyperref}
\hypersetup{
  colorlinks,
  linkcolor={red!50!black},
  citecolor={blue!50!black},
  urlcolor={blue!80!black}
}

% Mathematics

% Formatting 

\setlength{\parindent}{0pt}
\setlength{\parskip}{1em}

% TOC

\setcounter{tocdepth}{3}
\renewcommand{\thesection}{}
\renewcommand{\thesubsection}{}
\renewcommand{\thesubsubsection}{}

% Fancy

\fancypagestyle{plain}{%
\fancyhf{}
\setlength{\headheight}{2.5em}
\setlength{\headsep}{1em}
\renewcommand{\headrulewidth}{0pt}
\fancyhead[R]{%
\small Ass. 3, Week 38; Logic in CS 2014 \\
Karl D. Asmussen, \texttt{twk181}, 1992--11--27}
\fancyfoot[R]{\isodash{--}\isodate\today}
\fancyfoot[L]{Page \thepage\ of \pageref{Fin}}
}
\pagestyle{plain}

% Document proper

\begin{document}
\thispagestyle{empty}

\begin{center}
\textbf{\Huge Compilers}\\
\huge --- Group Assignment ---
\end{center}
\vspace{2em}

\begin{center}
\begin{minipage}{0.7\textwidth}
\LARGE Name: Karl D. Asmussen\\[1em]
Birthday: 1992--11--27\\[1em]
Student identifier: \texttt{twk181} \\[1em]
Faculty: DIKU
\end{minipage}
\end{center}

\pagebreak

\setcounter{page}{1}
\tableofcontents

\pagebreak
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{Group Dynamics}

My group has unfortunately fallen apart quite hard. First Adrian left due to stress,
then Bertram and myself drifted apart in the fogs of depressive apathy.

Fun.

As a result, I am handing in this assignment myself. I hope that is acceptable.

\section{Optionals and Tests}

I have implemented a solution to the shadowing problem, but not array comprehensions.

My build passes all the included tests, except for the array cromprehension one, and
I have made de-bugging inquries myself (although not implemented them as genuine tests,
because I couldn't make the test-writing work well with my development cycle, primarily
owing to SML's type system ridding me of almost all bugs, and because writing post-hoc
tests is disingenuine.)

\section{No Shortcuts}

For the large part, I have adopted a policy of not using shortcuts of reducing one
structure of FASTO to another in the compilation process. For instance, rewriting
the short-circuiting logic operators into IF-statements. This is largely to challenge
myself.

For Task 1, the implementation of arithmetic operators is fairly simple. In the
interpreter, I default to the builtin operations in SML, but notice in the constant
folding, that I have implemented the common algebraic rules of arithmetic, even
for addition and subtraction.

In the code generation, I found that boolean negation can be efficiently implemented with the
\texttt{slti} instruction, checking if the boolean value is less than 1.

Further, I have hand-implemented the short-circuiting nature of boolean conjunction and disjunction,
althoug I admit I haven't made inquries as to their code-generation friendlyness w.r.t. register
allocation.

A bug that came around to bite me in the arse was code-copying errors. First wrestle I had with
it was when division was translated into multiplication in the Type Checker.

For Task 2, I implemented \texttt{scan} as a straight-forward re-write of \texttt{reduce},
and then modified that to become \texttt{filter}, because \texttt{map}'s code is abhorrently
ugly to poke around.

For filter, I had some trouble with getting correct results, and the following list of
caveats nicely summarizes it:
\begin{itemize}
    \item \textit{Don't muck with the size register, as it is used in determining the
        number of iterations.}

        I had a significant amount of trouble at first, because I thought I could decrement
        the size counter when the predicate return false, thereby retaining a count of
        the number of elements in the new array.

    \item \textit{Remember to increment the iteration-count register before branching
        on the predicate's return value.}

        This led to an interesting series of bugs, where the output array was the
        same size as the input array.
    \item \textit{Adjusting the length of the resultant array can be computed from
        the destination pointer.}

        This allowed me to save a register, and led to another interesting series
        of bugs where the array were four elements longer, and four times larger
        than it should have been.
\end{itemize}

But before any of that, I had the problem that I had copied filter's code off map's
in the type checker, so I spent a good while pondering what was going on, before
figuring out exactly why I got a quite mangled array back.

For Task 3, I decided to write the type-cecking for lambda funargs by hand instead
of deferring to constructing a function declaration. This went well. Compiling the
lambda itself was an interesting mental challenge, but resulted in very little code.

Lastly in Task 4, I noticed while playing around with the \texttt{-p} option on
the fasto compiler, that extraneous \texttt{let} bindings --- those that could
be reduced to a constant or copy-propagation --- were left in, taking up resources
in the code generation, and made it so they were removed while preserving semantic
equivalence.

Implementing shadowing-proofing merely consisted of removing any copied variable's
propagation from the table, thereby allowing for instance, the problematic program
in the report, to compile successfully.

\section{Further improvements?}

In addition to implementing array comprehensions, a number of interesting optimizations
could be made in this language:

\begin{itemize}
    \item Rewriting \verb+if not X then A else B+ into \verb+if X then B else A+
    \item Adding rewrite rules for De-Morgan's laws.
    \item Rewrite rules to minimize arithmetic expressions (factorizing/distributing
        as appropriate to minimize the number of calculations needed.)
\end{itemize}

Further, it could do well with a modulus operator.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\appendix %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\newpage %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\phantomsection %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\addcontentsline{toc}{section}{Appendix} %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\fancypagestyle{plain}{% %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\fancyhf{} %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\setlength{\headheight}{2.5em} %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\setlength{\headsep}{1em} %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\renewcommand{\headrulewidth}{0pt} %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\fancyhead[R]{% %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\small Ass. 3, Week 38; Logic in CS 2014 \\ %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Karl D. Asmussen, \texttt{twk181}, 1992--11--27} %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\fancyfoot[R]{\isodash{--}\isodate\today} %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\fancyfoot[L]{Page \thepage\ of \pageref{Fin}} %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\fancyhead[L]{\scriptsize Appendix} %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
} %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\pagestyle{plain} %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{Code Listings of Interest}

\texttt{CopyConstPropFold.sml}, lines 51--64. My changes made to the
Minus case of \texttt{copyConstPropFoldExp}.
\begin{lstlisting}
      | Minus (e1, e2, pos) =>
        let val e1' = copyConstPropFoldExp vtable e1
            val e2' = copyConstPropFoldExp vtable e2
        in case (e1', e2') of
               (* added negation case, fixed order of patterns *)
               (Constant (IntVal 0, _), _) =>
               Negate (e2', pos)
             | (_, Constant (IntVal 0, _)) =>
               e1'
             | (Constant (IntVal x, _), Constant (IntVal y, _)) =>
               Constant (IntVal (x - y), pos) (* fixed: was x + y *)
             | _ =>
               Minus (e1', e2', pos)
        end
\end{lstlisting}
This is a partial implementation of common algebraic invariants of minus,
$x - 0 = x, 0 - x = -x$ and provides neater
(if not more efficent) code under \texttt{fasto -p c}.

\texttt{CopyConstPropFold.sml}, lines 168--184. Implementation of the
\texttt{Times} case in \texttt{copyConstPropFoldExp}
\begin{lstlisting}
      | Times (e1, e2, pos) =>
        let val e1' = copyConstPropFoldExp vtable e1
            val e2' = copyConstPropFoldExp vtable e2
        in case (e1', e2') of
               (Constant (IntVal 0, _), _) =>
               Constant (IntVal 0, pos)
             | (_, Constant (IntVal 0, _)) =>
               Constant (IntVal 0, pos)
             | (Constant (IntVal 1, _), _) =>
               e2'
             | (_, Constant (IntVal 1, _)) =>
               e1'
             | (Constant (IntVal x, _), Constant (IntVal y, _)) =>
               Constant (IntVal (x*y), pos)
             | _ =>
               Times (e1', e2', pos)
        end
\end{lstlisting}
Implements the identities $x\times0=0, x\times1=x$.

Similar identities have been added to the \texttt{Plus} case and
implemented in the \texttt{Division}, \texttt{And}, and \texttt{Or}
cases.

\texttt{CodeGen}, lines 645--649. \texttt{Not} case in \texttt{compileExp}:
\begin{lstlisting}
  | Not (e, pos) =>
      let val t = newName "not"
          val code = compileExp e vtable t
      in code @ [Mips.SLTI (place, t, makeConst 1)]
      end
\end{lstlisting}
Using \texttt{slti} to perform boolean negation after C-style booleans: 0 is false, everything
else is true.


\texttt{CodeGen.sml}, lines 645--649. \texttt{And} and \texttt{Or} cases in \texttt{compileExp}:
\begin{lstlisting}
  | And (e1, e2, pos) =>
      let val t1 = newName "and_L"
          val t2 = newName "and_R"
          val code1 = compileExp e1 vtable t1
          val code2 = compileExp e2 vtable t2
          val merge = newName "and_false"
      in code1 @
          [ Mips.LI (place, "0")
          , Mips.BEQ ("0", t1, merge) ]
          @ code2 @
          [ Mips.BEQ ("0", t2, merge)
          , Mips.LI (place, "1")
          , Mips.LABEL merge ]
      end
  | Or (e1, e2, pos) =>
      let val t1 = newName "or_L"
          val t2 = newName "or_R"
          val code1 = compileExp e1 vtable t1
          val code2 = compileExp e2 vtable t2
          val merge = newName "or_false"
      in code1 @
          [ Mips.LI (place, "1")
          , Mips.BEQ (place, t1, merge) ]
          @ code2 @
          [ Mips.SLT (place, t2, place)  (* BEQ has fallen through, place *)
          , Mips.SLT (place, place, "0") (* is still 1, compare, negate *)
          , Mips.LABEL merge ]
      end
\end{lstlisting}
Custom implentation of short-circuiting behavior.

\texttt{CodeGen}, lines 691--746. \texttt{Scan} case in \texttt{compileExp}:
\begin{lstlisting}
  | Scan (binop, acc_exp, arr_exp, tp, pos) =>
        let val arr_reg  = newName "arr_reg"   
            val size_reg = newName "size_reg"  
            val i_reg    = newName "ind_var"   
            val tmp_reg  = newName "tmp_reg"   
            val loop_beg = newName "loop_beg"
            val loop_end = newName "loop_end"

            val acc_reg = newName "acc_reg" (* need accumulator separate from place *)

            val arr_code = compileExp arr_exp vtable arr_reg
            val header1 = [ Mips.LW(size_reg, arr_reg, "0") ]

            val acc_code = compileExp acc_exp vtable acc_reg

            val dest_reg = newName "dest_reg"
            val alloc_array = [ Mips.ADDI( tmp_reg, size_reg, "1" ) ]
                            @ dynalloc( tmp_reg, place, tp )
                            @ [ Mips.MOVE( dest_reg, place ) ]

            val load_code =
                case getElemSize tp of
                    One =>  [ Mips.LB   (tmp_reg, arr_reg, "0")
                            , Mips.ADDI (arr_reg, arr_reg, "1") ]
                  | Four => [ Mips.LW   (tmp_reg, arr_reg, "0")
                            , Mips.ADDI (arr_reg, arr_reg, "4") ]
            val store_code =
                case getElemSize tp of
                    One =>  [ Mips.SB   (acc_reg, dest_reg, "0")
                            , Mips.ADDI (dest_reg, dest_reg, "1") ]
                  | Four => [ Mips.SW   (acc_reg, dest_reg, "0")
                            , Mips.ADDI (dest_reg, dest_reg, "4") ]

            val loop_code =
                [ Mips.ADDI(arr_reg, arr_reg, "4")
                , Mips.ADDI(dest_reg, dest_reg, "4")
                , Mips.MOVE(i_reg, "0")
                , Mips.LABEL(loop_beg) ]
                @ store_code @
                [ Mips.SUB(tmp_reg, i_reg, size_reg)
                , Mips.BGEZ(tmp_reg, loop_end) ]

            val apply_code =
                applyFunArg(binop, [acc_reg, tmp_reg], vtable, acc_reg, pos)

        in arr_code
         @ header1
         @ acc_code
         @ alloc_array
         @ loop_code
         @ load_code
         @ apply_code
         @ [ Mips.ADDI(i_reg, i_reg, "1")
           , Mips.J loop_beg
           , Mips.LABEL loop_end ]
        end
\end{lstlisting}
Modified version of \texttt{Reduce}. Notice that \verb+store_code+ falls before
\verb+load_code+ (by virtue of being inside \verb+loop_code+) so as to effect the
initial accumulator value being the first in the resultant array.

\texttt{CodeGen.sml} line 757--822. \texttt{Filter} case in \texttt{compileExp}:
\begin{lstlisting}
    | Filter (farg, arr_exp, tp, pos) =>
        let val arr_reg  = newName "arr_reg"   
            val size_reg = newName "size_reg"  
            val acc_reg = newName "acc_reg"
            val i_reg    = newName "ind_var"   
            val tmp_reg  = newName "tmp_reg"   
            val loop_beg = newName "loop_beg"
            val loop_end = newName "loop_end"

            val arr_code = compileExp arr_exp vtable arr_reg
            val header1 = [ Mips.LW(size_reg, arr_reg, "0") ]

            val dest_reg = newName "dest_reg"
            val alloc_array = dynalloc( size_reg, place, tp )
                            @ [ Mips.MOVE( dest_reg, place ) ]

            val load_code =
                case getElemSize tp of
                    One =>  [ Mips.LB   (acc_reg, arr_reg, "0")
                            , Mips.ADDI (arr_reg, arr_reg, "1") ]
                  | Four => [ Mips.LW   (acc_reg, arr_reg, "0")
                            , Mips.ADDI (arr_reg, arr_reg, "4") ]
            val store_code =
                case getElemSize tp of
                    One =>  [ Mips.SB   (acc_reg, dest_reg, "0")
                            , Mips.ADDI (dest_reg, dest_reg, "1") ]
                  | Four => [ Mips.SW   (acc_reg, dest_reg, "0")
                            , Mips.ADDI (dest_reg, dest_reg, "4") ]
            
            val loop_code =
                [ Mips.ADDI(arr_reg, arr_reg, "4")
                , Mips.ADDI(dest_reg, dest_reg, "4")
                , Mips.MOVE(i_reg, "0")
                , Mips.LABEL(loop_beg)
                , Mips.SUB(tmp_reg, i_reg, size_reg)
                , Mips.BGEZ(tmp_reg, loop_end) ]
            
            val filter_true = newName "filter_true"
            val apply_code =
                applyFunArg(farg, [acc_reg], vtable, tmp_reg, pos) @
                [ Mips.ADDI(i_reg, i_reg, "1")
                , Mips.BEQ (tmp_reg, "0", loop_beg) ]

            val loop_end =
              [ Mips.J loop_beg
              , Mips.LABEL loop_end
              , Mips.SUB( dest_reg, dest_reg, place )
              , Mips.LI(tmp_reg, "4")
              , Mips.SUB( dest_reg, dest_reg, tmp_reg ) ]

            val compute_size =
              case getElemSize tp of
                    One => [Mips.SW(dest_reg, place, "0")]
                  | Four => [ Mips.SRA( dest_reg, dest_reg, "2" )
                            , Mips.SW(dest_reg, place, "0")]

        in arr_code
         @ header1
         @ alloc_array
         @ loop_code
         @ load_code
         @ apply_code
         @ store_code
         @ loop_end
         @ compute_size
        end
\end{lstlisting}
Heavily based on \texttt{Scan}, Notice in \verb+compute_size+ how shifts are used 
to effect a division by four.

\texttt{TypeChecker.sml} lines 364--373. \texttt{Lambda} case in \texttt{checkFunArg}:
\begin{lstlisting}
  | checkFunArg (In.Lambda (ret, params, exp, pos), vtab, ftab, pos_x) =
      let fun addParam (Param (pname, ty), ptable) =
              case SymTab.lookup pname ptable of
                    SOME _ => raise Error ("Multiple definitions of parameter name " ^ pname, pos)
                  | NONE   => SymTab.bind pname ty ptable
          val ptable = foldl addParam (SymTab.empty()) params
          val (exp_t, exp_dec) = checkExp ftab (SymTab.combine ptable vtab) exp
          val ret_t = unifyTypes pos (exp_t, ret)
          val args_t = map (fn Param(_, ty) => ty) params
      in (Out.Lambda (ret_t, params, exp_dec, pos), ret_t, args_t) end
\end{lstlisting}
Hand-written type checking, not depending on \texttt{checkFunWithVtable}, although it does
copy the addParam function.

\texttt{CodeGen.sml} lines 843--846. \texttt{Lambda} case in \texttt{compileFunArgWithVtable}:
\begin{lstlisting}
    | applyFunArg (Lambda (ret, args, exp, _), regs, vtable, place, _) =
      let fun addParam ((Param (n, _), r), st) = SymTab.bind n r st
          val vtable' = foldl addParam vtable (ListPair.zip (args, regs))
      in compileExp exp vtable' place end
\end{lstlisting}
A neat little piece of code.

\texttt{CopyConstPropFold.sml}, lines 98--122. \texttt{Let} case of
\texttt{copyConstPropFoldExp}:
\begin{lstlisting}
      | Let (Dec (name, e, decpos), body, pos) =>

        (* TODO TASK 4: This case currently does nothing.

         You must extend this case to expand the vtable' with whatever
         Propagatee that you can get out of e'.  That is, inspect e'
         to see whether it is a constant or variable, and if so,
         insert the appropriate Propagatee value in vtable. *)

        let val e' = copyConstPropFoldExp vtable e
            val (vtable', prop) =
                case e' of 
                     Constant(v, _) => (SymTab.bind name (ConstProp v) vtable, true)
                   | Var(n, _) =>
                       (SymTab.bind name (VarProp n)
                          (SymTab.remove n vtable) (* remove to prevent shadowing errors *)
                       , true)
                   | _ => (vtable, false)
        in if prop then
                (* bonus, if we can propagate it, the Let is extraneous *)
                copyConstPropFoldExp vtable' body
           else Let (Dec (name, e', decpos),
                copyConstPropFoldExp vtable' body,
                pos)
        end
\end{lstlisting}
The secret to properly handling shadowing is to remove copy propagated variables from the
symbol table. In the case that it is possible to propagate the declaration, the let is extraneous
and is discarded.

% \phantomsection
% \addcontentsline{toc}{section}{List of Figures}
% \listoffigures

% \phantomsection
% \addcontentsline{toc}{section}{List of Tables}
% \listoftables

% \phantomsection
% \addcontentsline{toc}{section}{\bibname}
% \begin{thebibliography}{9}
% \end{thebibliography}

\vfill
\begin{center}
  \textbf{\textit{Fin.}}
  \label{Fin}
\end{center}

\end{document}
